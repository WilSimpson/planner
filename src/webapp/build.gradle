plugins {
  id 'java'
  id 'com.github.node-gradle.node'
}

task startAngularApp(type: NpxTask) {
  dependsOn npmInstall
  command = 'ng'
  args = ['serve']
}

task lintAngularApp(type: NpxTask) {
  dependsOn npmInstall
  command = 'ng'
  args = ['lint']
  inputs.dir("src")
  inputs.dir("node_modules")
  inputs.files("angular.json", ".browserslistrc", "tsconfig.json", "tsconfig.app.json", "tsconfig.spec.json",
    "tslint.json")
  outputs.upToDateWhen { true }
}

task buildAngularApp(type: NpxTask) {
  dependsOn npmInstall, tasks.lintAngularApp
  command = 'ng'
  args = ['build', '--prod']
  inputs.files('package.json', 'package-lock.json', 'angular.json', '.browserslistrc', 'tsconfig.json', 'tsconfig.app.json')
  inputs.dir(project.fileTree("src").exclude('**/*.spec.ts'))
  inputs.dir(fileTree('node_modules').exclude('.cache'))
  outputs.dir("${project.buildDir}/webapp")
}

task testAngularApp(type: NpxTask) {
  dependsOn npmInstall, tasks.lintAngularApp
  command = 'ng'
  args = ['test']
  inputs.dir("src")
  inputs.dir("node_modules")
  inputs.files("angular.json", ".browserslistrc", "tsconfig.json", "tsconfig.spec.json", "karma.conf.js")
  outputs.upToDateWhen { true }
}


node {
  // Whether to download and install a specific Node.js version or not
  // If false, it will use the globally installed Node.js
  // If true, it will download node using above parameters
  // Note that npm is bundled with Node.js
  download = true

  // Version of node to download and install (only used if download is true)
  // It will be unpacked in the workDir
  version = "12.18.3"

  // Version of npm to use
  // If specified, installs it in the npmWorkDir
  // If empty, the plugin will use the npm command bundled with Node.js
  npmVersion = "6.14.4"

  // Version of Yarn to use
  // Any Yarn task first installs Yarn in the yarnWorkDir
  // It uses the specified version if defined and the latest version otherwise (by default)
  yarnVersion = ""

  // Base URL for fetching node distributions
  // Only used if download is true
  // Change it if you want to use a mirror
  // Or set to null if you want to add the repository on your own.
  distBaseUrl = "https://nodejs.org/dist"

  // The npm command executed by the npmInstall task
  // By default it is install but it can be changed to ci
  npmInstallCommand = "install"

  // The directory where Node.js is unpacked (when download is true)
  workDir = file("${project.projectDir}/.gradle/nodejs")

  // The directory where npm is installed (when a specific version is defined)
  npmWorkDir = file("${project.projectDir}/.gradle/npm")

  // The directory where yarn is installed (when a Yarn task is used)
  yarnWorkDir = file("${project.projectDir}/.gradle/yarn")

  // The Node.js project directory location
  // This is where the package.json file and node_modules directory are located
  // By default it is at the root of the current project
  nodeModulesDir = file("${project.projectDir}")

  // Whether the plugin automatically should add the proxy configuration to npm and yarn commands
  // according the proxy configuration defined for Gradle
  // Disable this option if you want to configure the proxy for npm or yarn on your own
  // (in the .npmrc file for instance)
  //useGradleProxySettings = true
}

